plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.+'
	id 'maven-publish'
}
apply from: "${rootProject.projectDir}/gradle/fabric.gradle"
apply from: "${rootProject.projectDir}/gradle/java.gradle"

archivesBaseName = project.archives_base_name
version = getVersion()
group = project.maven_group

repositories {
	maven {
		url = 'https://jitpack.io'
		content {
			includeGroup "com.github.Chocohead"
		}
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url = 'https://cursemaven.com'
		content {
			includeGroup "curse.maven"
		}
	}
}

loom {

	accessWidenerPath = file("src/main/resources/twm.accesswidener")
}

configurations {
	includeModImplementation
	includeImplementation

	include.extendsFrom includeImplementation
	include.extendsFrom includeModImplementation

	implementation.extendsFrom includeImplementation
	implementation.extendsFrom includeModImplementation
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Fabric-ASM
	modImplementation "com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"
	include "com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"

	// only affects runClient, does not affect gradlew build. add -PuseThirdPartyMods=false to not use these
	if (findProperty("useThirdPartyMods") != "false") {
		modRuntimeOnly "maven.modrinth:modmenu:${modmenu_version}"
		modRuntimeOnly "maven.modrinth:ferrite-core:${ferritecore_version}"
		modRuntimeOnly "maven.modrinth:lazydfu:${lazydfu_version}"
		modRuntimeOnly "maven.modrinth:starlight:${starlight_version}"
		//modRuntimeOnly "maven.modrinth:indium:1.0.6+mc1.19"
		//modRuntimeOnly "maven.modrinth:lithium:mc1.19-0.8.1"
		//modRuntimeOnly "maven.modrinth:sodium:mc1.19-0.4.2"
		modRuntimeOnly "maven.modrinth:entityculling:${entityculling_version}"
		//modRuntimeOnly "maven.modrinth:c2me-fabric:0.2.0+alpha.8.32+1.19.1-rc3"
		modRuntimeOnly "maven.modrinth:ksyxis:${ksyxis_version}"
		//modRuntimeOnly "maven.modrinth:iris:1.19.x-v1.2.5"
		modRuntimeOnly "maven.modrinth:memoryleakfix:${memoryleakfix_version}"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	options.release.set(17)
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
	options.fork = true
	options.incremental = true
}

tasks.withType(Test).configureEach {
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

java {
	withSourcesJar()
}

jar {
	from "${rootProject.projectDir}/LICENSE.txt"
}

String getVersion() {
	String version = "${project.mod_version}+MC_${project.minecraft_version}"

	if (findProperty("releaseType") != "stable") {
		version += "-unstable"
	}

	return version
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		mavenCentral()
	}

	return version
}
